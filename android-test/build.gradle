apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "io.ably.lib.test"
        minSdkVersion 8
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "class", "io.ably.lib.test.rest.RestSuite,io.ably.lib.test.realtime.RealtimeSuite"
        testInstrumentationRunnerArgument "timeout_msec", "150000"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
  resolutionStrategy.force 'com.android.support:support-annotations:23.0.0'
}

/* Fix for android test logging. Source: https://code.google.com/p/android/issues/detail?id=182307 */
tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
    task.doFirst {
        logging.level = LogLevel.INFO
    }
    task.doLast {
        logging.level = LogLevel.LIFECYCLE
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'io.ably:ably-android:0.8.4'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-all:1.3'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.nanohttpd:nanohttpd:2.2.0'
    androidTestCompile 'org.nanohttpd:nanohttpd-nanolets:2.3.0'
    androidTestCompile 'org.nanohttpd:nanohttpd-websocket:2.2.0'
    androidTestCompile 'org.mockito:mockito-core:2.0.17-beta'
}

configurations {
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
}

task copyAssetFiles (type: Copy) {
  description = 'Copies assets from the common library...'
  from new File(project(':lib').projectDir, '/src/test/resources')
  into new File(project.projectDir, '/src/androidTest/assets')
  include('**/*.json')
}

android.testVariants.all{ variant ->
    variant.mergeAssets.dependsOn copyAssetFiles
}
